ВЫБРАТЬ РАЗЛИЧНЫЕ
		КПВ_СрокиРеакцииДоставкиПоПунктам.ГруппаПунктовKPI,
		СоставГруза.Ссылка КАК Лот,
		ПВ_ИсторияАвтомобиля.Контрагент,
		ПВ_ИсторияАвтомобиля.Автомобиль.Model КАК Модель,
		ПВ_ИсторияАвтомобиля.Автомобиль,
		ПВ_ИсторияАвтомобиляСУчетомСквозных.ДатаЗаявки,
		ПВ_ИсторияАвтомобиляСУчетомСквозных.ДатаДоступности,
		ПВ_ИсторияАвтомобиляСУчетомСквозных.ДатаЗагрузкиФакт,
		ПВ_ИсторияАвтомобиляСУчетомСквозных.ДатаРазгрузки,
		РАЗНОСТЬДАТ(ПВ_ИсторияАвтомобиляСУчетомСквозных.ДатаДоступности, ПВ_ИсторияАвтомобиляСУчетомСквозных.ДатаЗагрузкиФакт, ДЕНЬ) КАК ФактическийСрокРеакции,
		КПВ_СрокиРеакцииДоставкиПоПунктам.СрокРеакции,
		КПВ_СрокиРеакцииДоставкиПоПунктам.СрокРеакции - РАЗНОСТЬДАТ(ПВ_ИсторияАвтомобиляСУчетомСквозных.ДатаДоступности, ПВ_ИсторияАвтомобиляСУчетомСквозных.ДатаЗагрузкиФакт, ДЕНЬ) КАК РазностьСрокаРеакции,
		РАЗНОСТЬДАТ(ПВ_ИсторияАвтомобиляСУчетомСквозных.ДатаЗагрузкиФакт, ПВ_ИсторияАвтомобиляСУчетомСквозных.ДатаРазгрузки, ДЕНЬ) КАК ФактическоеВремяДоставки,
		КПВ_СрокиРеакцииДоставкиПоПунктам.СрокДоставки,
		КПВ_СрокиРеакцииДоставкиПоПунктам.СрокДоставки - РАЗНОСТЬДАТ(ПВ_ИсторияАвтомобиляСУчетомСквозных.ДатаЗагрузкиФакт, ПВ_ИсторияАвтомобиляСУчетомСквозных.ДатаРазгрузки, ДЕНЬ) КАК РазностьСрокаДоставки,
        ИШАдресаТТНПогрузка.Город КАК ПунктЗагрузки,
        ПВ_ИсторияАвтомобиляСУчетомСквозных.АдресПогрузки,
		ИШАдресаТТНДоставка.Город КАК ПунктДоставки,
		ПВ_ИсторияАвтомобиля.АдресДоставки,
		ПВ_ИсторияАвтомобиля.КодДилера,
		СоставГруза.Ссылка.Экспедитор КАК Экспедитор
ИЗ РегистрСведений.ПВ_ИсторияАвтомобиля КАК ПВ_ИсторияАвтомобиля
	
	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.уатГрузы.СоставГруза как СоставГруза
		ПО ПВ_ИсторияАвтомобиля.Период =  СоставГруза.Ссылка.ДатаЗагрузки
		И ПВ_ИсторияАвтомобиля.Автомобиль = СоставГруза.Автомобиль
	
	 
	 // Блок для сквозных перевозок и стандартных дат    
    ЛЕВОЕ СОЕДИНЕНИЕ( Выбрать ПВ_ИсторияАвтомобиля.Автомобиль,
				        ПВ_ИсторияАвтомобиля.Период,
				        ВЫБОР КОГДА ПВ_ИсторияАвтомобиля.СквознаяПеревозка = ЗНАЧЕНИЕ(Перечисление.КПВ_СквознаяПеревозка.Конец) ТОГДА
				              СоставГрузаСквозной.ДатаЗагрузкиФакт                   
				        ИНАЧЕ
				              СоставГруза.ДатаЗагрузкиФакт
				        КОНЕЦ КАК ДатаЗагрузкиФакт,
				        ВЫБОР КОГДА ПВ_ИсторияАвтомобиля.СквознаяПеревозка = ЗНАЧЕНИЕ(Перечисление.КПВ_СквознаяПеревозка.Конец) ТОГДА
				              ПерваяИсторияСквознойПеревозки.АдресПогрузки          
				        ИНАЧЕ
				              ПВ_ИсторияАвтомобиля.АдресПогрузки
				        КОНЕЦ КАК АдресПогрузки,        
				        // 1. есть сквозная, нет даты доступности 2. есть сквозная - дата 3.  нет сквозной нет даты 4. нет сквозной есть дата доступности
				        ВЫБОР КОГДА ПВ_ИсторияАвтомобиля.СквознаяПеревозка = ЗНАЧЕНИЕ(Перечисление.КПВ_СквознаяПеревозка.Конец) И ПерваяИсторияСквознойПеревозки.ДатаДоступности = ДатаВремя(1,1,1) ТОГДА
				              ПерваяИсторияСквознойПеревозки.ДатаЗаявки
				        КОГДА ПВ_ИсторияАвтомобиля.СквознаяПеревозка = ЗНАЧЕНИЕ(Перечисление.КПВ_СквознаяПеревозка.Конец) ТОГДА
				              ПерваяИсторияСквознойПеревозки.ДатаДоступности
				        КОГДА ПВ_ИсторияАвтомобиля.ДатаДоступности = ДатаВремя(1,1,1) ТОГДА
				        	  ПВ_ИсторияАвтомобиля.ДатаЗаявки
				        ИНАЧЕ
				              ПВ_ИсторияАвтомобиля.ДатаДоступности			          
				        КОНЕЦ КАК ДатаДоступности,
				        
				        ПВ_ИсторияАвтомобиля.АдресДоставки,
				        СоставГруза.ДатаРазгрузки,
				        ВЫБОР КОГДА ПВ_ИсторияАвтомобиля.СквознаяПеревозка = ЗНАЧЕНИЕ(Перечисление.КПВ_СквознаяПеревозка.Конец) ТОГДА
				              ПерваяИсторияСквознойПеревозки.ДатаЗаявки                   
				        ИНАЧЕ
				              ПВ_ИсторияАвтомобиля.ДатаЗаявки
				        КОНЕЦ КАК ДатаЗаявки        
						ИЗ РегистрСведений.ПВ_ИсторияАвтомобиля КАК ПВ_ИсторияАвтомобиля
						   ЛЕВОЕ СОЕДИНЕНИЕ Справочник.уатГрузы.СоставГруза КАК СоставГруза
						                    ПО ПВ_ИсторияАвтомобиля.Автомобиль =  СоставГруза.Автомобиль
						                    И  ПВ_ИсторияАвтомобиля.Период = СоставГруза.Ссылка.ДатаЗагрузки
						   ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПВ_ИсторияАвтомобиля КАК ПерваяИсторияСквознойПеревозки
						                    ПО ПерваяИсторияСквознойПеревозки.Автомобиль = ПВ_ИсторияАвтомобиля.Автомобиль
				   	 						И ПерваяИсторияСквознойПеревозки.Период В (ВЫБРАТЬ МАКСИМУМ(Период)
				      										 ИЗ РегистрСведений.ПВ_ИсторияАвтомобиля
				     										 ГДЕ Период < ПВ_ИсторияАвтомобиля.Период
				      										 И СквознаяПеревозка = Значение(Перечисление.КПВ_СквознаяПеревозка.Начало)
				     										 И Автомобиль = ПВ_ИсторияАвтомобиля.Автомобиль)
						   ЛЕВОЕ СОЕДИНЕНИЕ Справочник.уатГрузы.СоставГруза КАК СоставГрузаСквозной
						                    ПО ПВ_ИсторияАвтомобиля.Автомобиль =  СоставГрузаСквозной.Автомобиль
						                    И  ПерваяИсторияСквознойПеревозки.Период = СоставГрузаСквозной.Ссылка.ДатаЗагрузки
						) КАК ПВ_ИсторияАвтомобиляСУчетомСквозных
		ПО
		    ПВ_ИсторияАвтомобиляСУчетомСквозных.Автомобиль = ПВ_ИсторияАвтомобиля.Автомобиль
		    И ПВ_ИсторияАвтомобиляСУчетомСквозных.Период = ПВ_ИсторияАвтомобиля.Период
	// конец блока сквозных перевозок   
	
	
	
	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИШ_АдресаТТН КАК ИШАдресаТТНПогрузка
		ПО ИШАдресаТТНПогрузка.Наименование = ПВ_ИсторияАвтомобиляСУчетомСквозных.АдресПогрузки
		И  НЕ ИШАдресаТТНПогрузка.ПометкаУдаления
	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИШ_АдресаТТН КАК ИШАдресаТТНДоставка
		ПО ИШАдресаТТНДоставка.Наименование = ПВ_ИсторияАвтомобиля.АдресДоставки
		И  НЕ ИШАдресаТТНДоставка.ПометкаУдаления
	ЛЕВОЕ СОЕДИНЕНИЕ (Выбрать 
						КПВ_СрокиРеакцииДоставкиПоПунктам.Пункт1,
						КПВ_СрокиРеакцииДоставкиПоПунктам.Пункт2,
						КПВ_СрокиРеакцииДоставкиПоПунктам.Контрагент,
						ВЫБОР КОГДА КПВ_СрокиРеакцииДоставкиПоПунктам.ГруппаПунктовKPI = Значение(Справочник.КПВ_ГруппыПунктовДляKPI.ПустаяСсылка) ТОГДА
						      КПВ_СрокиРеакцииДоставкиПоПунктам.Пункт1.Наименование + " - Остальные" 
						ИНАЧЕ  КПВ_СрокиРеакцииДоставкиПоПунктам.ГруппаПунктовKPI КОНЕЦ КАК ГруппаПунктовKPI,
						КПВ_СрокиРеакцииДоставкиПоПунктам.СрокРеакции, 
						КПВ_СрокиРеакцииДоставкиПоПунктам.СрокДоставки
					   ИЗ РегистрСведений.КПВ_СрокиРеакцииДоставкиПоПунктам КАК КПВ_СрокиРеакцииДоставкиПоПунктам) КАК КПВ_СрокиРеакцииДоставкиПоПунктам
	    ПО КПВ_СрокиРеакцииДоставкиПоПунктам.Пункт1 = ИШАдресаТТНПогрузка.Город
	    И  КПВ_СрокиРеакцииДоставкиПоПунктам.Пункт2 = ИШАдресаТТНДоставка.Город
	    И  ПВ_ИсторияАвтомобиля.Контрагент = КПВ_СрокиРеакцииДоставкиПоПунктам.Контрагент



ГДЕ
    ПВ_ИсторияАвтомобиляСУчетомСквозных.ДатаЗагрузкиФакт МЕЖДУ &НачПериода И &КонПериода
    И ПВ_ИсторияАвтомобиля.СквознаяПеревозка <>  ЗНАЧЕНИЕ(Перечисление.КПВ_СквознаяПеревозка.Начало)
    И ПВ_ИсторияАвтомобиля.СквознаяПеревозка <>  ЗНАЧЕНИЕ(Перечисление.КПВ_СквознаяПеревозка.Середина)
